apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'

tasks.findByPath("artifactoryPublish")?.enabled = false
sonarRunner {
	skipProject = true
}

task integrationTomcatRunWar(type: com.bmuschko.gradle.tomcat.tasks.TomcatRun, dependsOn: war) {
	contextPath = ''
	daemon = true
	doFirst {
		// delay reserving ports to ensure they are still available
		def ports = reservePorts(3)
		httpPort = ports[0]
		ajpPort = ports[1]
		stopPort = ports[2]
	}
}

task integrationTomcatStop(type: com.bmuschko.gradle.tomcat.tasks.TomcatStop, dependsOn: war) {
	doFirst {
		stopPort = integrationTomcatRunWar.stopPort
	}
}

test {
	doFirst {
		def host = 'localhost:' + integrationTomcatRunWar.httpPort
		systemProperties['geb.build.baseUrl'] = 'http://' + host + integrationTomcatRunWar.contextPath + '/'
		systemProperties['geb.build.reportsDir'] = 'build/geb-reports'
	}
}

description = 'Spring MVC Test HtmlUnit Sample'

tasks.findByPath("artifactoryPublish")?.enabled = false

dependencies {
	def tomcatVersion = '7.0.61'
	tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
			"org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
	tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}")

	compile "org.springframework:spring-context:$springVersion",
			"org.springframework:spring-webmvc:$springVersion",
			"org.springframework.data:spring-data-jpa:1.5.1.RELEASE",
			"org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final",
			"org.hibernate:hibernate-entitymanager:4.3.4.Final",
			"org.hibernate:hibernate-validator:4.1.0.Final",
			"org.aspectj:aspectjrt:1.6.10",
			"javax.validation:validation-api:1.0.0.GA",
			"org.slf4j:slf4j-log4j12:$slf4jVersion",
			"org.slf4j:jcl-over-slf4j:$slf4jVersion",
			"org.hsqldb:hsqldb:2.3.2",
			"org.thymeleaf:thymeleaf:2.1.2.RELEASE",
			"org.thymeleaf:thymeleaf-spring4:2.1.2.RELEASE",
			"org.springframework.security:spring-security-web:$springSecurityVersion",
			"org.springframework.security:spring-security-config:$springSecurityVersion"

	providedCompile "javax.servlet:javax.servlet-api:$servletApiVersion"

	testCompile project(':spring-test-htmlunit'),
			"org.springframework:spring-test:$springVersion",
			"org.seleniumhq.selenium:selenium-htmlunit-driver:$seleniumVersion",
			"org.seleniumhq.selenium:selenium-support:$seleniumVersion",
			'org.gebish:geb-spock:0.9.2',
			'org.spockframework:spock-core:0.7-groovy-2.0',
			'org.spockframework:spock-spring:0.7-groovy-2.0',
			"junit:junit:$junitVersion",
			'org.easytesting:fest-assert-core:2.0M10',
			"org.mockito:mockito-all:$mockitoVersion",
			"org.codehaus.groovy:groovy:2.4.3",
			'org.easytesting:fest-assert:1.4',
			"org.springframework.security:spring-security-test:$springSecurityVersion"

	jacoco "org.jacoco:org.jacoco.agent:$jacocoVersion:runtime"
}

eclipse.wtp.component.contextPath = ''

test {
	dependsOn integrationTomcatRunWar
	finalizedBy integrationTomcatStop
}