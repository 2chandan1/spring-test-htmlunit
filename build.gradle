buildscript {
  repositories {
    maven { url "https://repo.spring.io/plugins-release" }
  }
  dependencies {
    classpath("org.springframework.build.gradle:propdeps-plugin:0.0.7")
    classpath('me.champeau.gradle:gradle-javadoc-hotfix-plugin:0.1')
  }
}

plugins {
  id 'org.asciidoctor.convert' version '1.5.2'
  id 'com.bluepapa32.watch' version '0.1.5'
  id 'org.kordamp.gradle.livereload' version '0.2.1'
  id 'com.bmuschko.tomcat' version '2.2.1'
}

ext.releaseBuild = version.endsWith('RELEASE')
ext.snapshotBuild = version.endsWith('SNAPSHOT')
ext.milestoneBuild = !(releaseBuild || snapshotBuild)

ext.gebVersion = '0.10.0'
ext.htmlunitVersion = '2.16'
ext.jacocoVersion = '0.7.4.201502262128'
ext.junitVersion ='4.11'
ext.mockitoVersion = '1.10.19'
ext.seleniumVersion = '2.45.0'
ext.servletApiVersion = '3.0.1'
ext.slf4jVersion = '1.7.10'
ext.spockVersion = '1.0-groovy-2.4'
ext.springVersion = '4.1.6.RELEASE'
ext.springSecurityVersion = '4.0.1.RELEASE'

ext.coreModuleProjects = [project('spring-test-htmlunit')]

apply plugin: 'sonar-runner'

sonarRunner {
    sonarProperties {
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.projectName", "Spring Test HtmlUnit"
        property "sonar.jacoco.reportPath", "${buildDir.name}/jacoco.exec"
        property "sonar.links.homepage", 'https://github.com/spring-projects/spring-test-htmlunit'
        property "sonar.links.ci", 'https://build.spring.io/browse/SPRINGTESTMVC-BSTMHTMLUNIT'
        property "sonar.links.issue", 'https://github.com/spring-projects/spring-test-htmlunit/issues'
        property "sonar.links.scm", 'https://github.com/spring-projects/spring-test-htmlunit'
        property "sonar.links.scm_dev", 'https://github.com/spring-projects/spring-test-htmlunit.git'
        property "sonar.java.coveragePlugin", "jacoco"
    }
}

configure(allprojects) {
  apply plugin: 'java'
  apply plugin: 'eclipse-wtp'
  apply plugin: 'idea'
  apply plugin: 'propdeps'
  apply plugin: 'propdeps-maven'
  apply plugin: 'propdeps-idea'
  apply plugin: 'propdeps-eclipse'

  apply from: "${rootProject.projectDir}/publish-maven.gradle"

  group = 'org.springframework'

  sourceCompatibility=1.6
  targetCompatibility=1.6

  [compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:none']

  test.systemProperty("java.awt.headless", "true")

  repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/libs-snapshot' }
  }

  configurations {
      jacoco //Configuration Group used by Sonar to provide Code Coverage using JaCoCo
  }

  configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
      if (details.requested.group == 'org.springframework' &&
              details.requested.name != project.findProject(':spring-test-htmlunit').name) {
        details.useVersion springVersion
      }
    }
  }


  idea {
    module {
      scopes.PROVIDED.plus += [configurations.provided]
      scopes.PROVIDED.plus += [configurations.optional]
    }
  }
  // servlet-api (2.5) and tomcat-servlet-api (3.0) classpath entries should not be
  // exported to dependent projects in Eclipse to avoid false compilation errors due
  // to changing APIs across these versions
  eclipse.classpath.file.whenMerged { classpath ->
    classpath.entries.findAll { entry -> entry.path.contains('servlet-api') }*.exported = false
  }
}

configure(allprojects) { subproject ->
  jar {
    manifest.attributes['Implementation-Title'] = subproject.name
    manifest.attributes['Implementation-Version'] = subproject.version
  }

  javadoc {
    options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
    options.author = true
    options.header = project.name
  }

  task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allJava
  }

  task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
  }

  artifacts {
    archives sourcesJar
    archives javadocJar
  }

  task listDependencies {
    doLast {
      println project.name
      configurations.testRuntime.collect { "    $it.path" }.sort().each { println it }
    }
  }

  test {
    doFirst {
        jvmArgs "-javaagent:${configurations.jacoco.asPath}=destfile=${buildDir}/jacoco.exec,includes=org.springframework.test.web.servlet.htmlunit.*"
    }
  }
}


configure(rootProject) {
  apply plugin: 'org.asciidoctor.gradle.asciidoctor'


  dependencies {
    testCompile project(':spring-test-htmlunit'),
            "org.springframework:spring-test:$springVersion",
            "org.springframework:spring-webmvc:$springVersion",
            "org.springframework.security:spring-security-web:$springSecurityVersion",
            "org.springframework.security:spring-security-config:$springSecurityVersion",
            "org.springframework.security:spring-security-test:$springSecurityVersion",
            "org.seleniumhq.selenium:selenium-htmlunit-driver:$seleniumVersion",
            "org.seleniumhq.selenium:selenium-support:$seleniumVersion",
            "javax.servlet:javax.servlet-api:$servletApiVersion",
            "junit:junit:$junitVersion"

    jacoco "org.jacoco:org.jacoco.agent:$jacocoVersion:runtime"
  }

  task configureAciidoctor << {
    configure(project.tasks.asciidoctor) {
      def docsTestSource = project.rootProject.sourceSets.test.java.srcDirs[0]
      def samplesMainSource = project(':mail-webapp').sourceSets.main.java.srcDirs[0]
      def samplesTestSource = project(':mail-webapp').sourceSets.test.java.srcDirs[0]
      def samplesTestSourceGroovy =
              project(':mail-webapp').sourceSets.test.groovy.srcDirs[0]

      inputs.dir docsTestSource
      inputs.dir samplesMainSource
      inputs.dir samplesTestSource
      inputs.dir samplesTestSourceGroovy

      options eruby : 'erubis'

      attributes   'docs-src-test-java'     : docsTestSource,
                   'samples-src-main-java'  : samplesMainSource,
                   'samples-src-test-java'  : samplesTestSource,
                   'samples-src-test-groovy': samplesTestSourceGroovy,

                   'selenium-version'       : seleniumVersion,
                   'spock-version'          : spockVersion,
                   'geb-version'            : gebVersion,

                   toc                      : '',
                   'toc-placement'          : 'preamble',
                   copycss                  : '',
                   icons                    : 'font',
                   'source-highlighter'     : 'prettify',
                   sectanchors              : '',
                   idprefix                 : '',
                   idseparator              : '-',
                   doctype                  : 'book',
                   numbered                 : '',
                   revnumber                : project.version,
                   version                  : project.version,
                   snapshot                 : snapshotBuild,
                   release                  : releaseBuild,
                   milestone                : milestoneBuild
    }
  }

  asciidoctor.dependsOn configureAciidoctor
  build.dependsOn asciidoctor


  watch {
    asciidoc {
      files fileTree('src')
      tasks 'asciidoctor'
    }
  }

  liveReload {
    docRoot project.projectDir.canonicalPath
  }

  task api(type: Javadoc) {
    group = 'Documentation'
    description = 'Generates aggregated Javadoc API documentation.'
    title = "${rootProject.description} ${version} API"
    options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
    options.author = true
    options.header = rootProject.description
    options.links(
      'http://docs.jboss.org/jbossas/javadoc/4.0.5/connector'
    )
    source coreModuleProjects.collect { project ->
      project.sourceSets.main.allJava
    }
    destinationDir = new File(buildDir, "api")
    classpath = files(coreModuleProjects.collect { project ->
      project.sourceSets.main.compileClasspath
    })
    maxMemory = '1024m'
  }

  task docsZip(type: Zip) {
    group = 'Distribution'
    classifier = 'docs'
    description = "Builds -${classifier} archive containing api and reference " +
      "for deployment at docs.spring.io"

    from (api) {
      into 'api'
    }

    from (asciidoctor) {
        into 'reference'
    }
  }

  task distZip(type: Zip, dependsOn: [docsZip]) {
    group = 'Distribution'
    classifier = 'dist'
    description = "Builds -${classifier} archive, containing all jars and docs, " +
          "suitable for community download page."

    def baseDir = "${project.name}-${project.version}";

    from('src/dist') {
      include 'readme.txt'
      include 'license.txt'
      include 'notice.txt'
      into "${baseDir}"
      expand(copyright: new Date().format('yyyy'), version: project.version)
    }

    from(zipTree(docsZip.archivePath)) {
      into "${baseDir}/docs"
    }

    coreModuleProjects.each { project ->
      into ("${baseDir}/libs") {
        from project.jar
        if (project.tasks.findByPath('sourcesJar')) {
          from project.sourcesJar
        }
        if (project.tasks.findByPath('javadocJar')) {
          from project.javadocJar
        }
      }
    }
  }

  configurations.archives.artifacts.clear()

  artifacts {
    archives docsZip
    archives distZip
  }

  task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts'
    gradleVersion = '1.11'
  }
}

def reservePorts(int count) {
    def sockets = []
    for(int i in 1..count) {
        sockets << new ServerSocket(0)
    }
    def result = sockets*.localPort
    sockets*.close()
    result
}
